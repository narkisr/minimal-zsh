#compdef lxc

# _lxd
#
# Copyright (c) 2017 endaaman
#
# This software may be modified and distributed under the terms
# of the MIT license. See the LICENSE file for details.

_lxc() {
  local context curcontext=$curcontext state line
  local selected_container
  declare -A opt_args

  local opt_common=(
    '(- *)'{-h,--help}'[Show help]' \
    '--debug[Enable debug mode]' \
    '--verbose[Enable verbose mode]' \
    '--force-local[Force using the local unix socket]' \
  )
  local opt_container_control=(
    '--all[Run command against all containers]' \
    '--stateful[Store the container state (only for stop)]' \
    '--stateless[Ignore the container state (only for start)]' \
  )

  _arguments -C \
    '--all[Print less common commands]' \
    '--version[Show client version]' \
    $opt_common \
    '1: :__lxc_commands' \
    '*:: :->args'
  case $state in
    (args)
      case $words[1] in
        (alias)
          case $words[2] in
            (remove|rename)
              _arguments -C \
                '2:containers:__lxc_aliases'
            ;;
          esac
          _arguments -C \
            '1: :__lxc_alias_commands' \
            $opt_common
        ;;
        (cluster)
          _arguments -C \
            '1: :__lxc_cluster_commands' \
            $opt_common
        ;;
        (config)
          case $words[2] in
            (get)
              _arguments -C \
                '2:containers:__lxc_containers_all'
            ;;
            (set)
              _arguments -C \
                '2:containers:__lxc_containers_all'
            ;;
            (unset)
              _arguments -C \
                '2:containers:__lxc_containers_all'
            ;;
            (show)
              _arguments -C \
                '2:containers:__lxc_containers_all'
            ;;
            (edit)
              _arguments -C \
                '2:containers:__lxc_containers_all'
            ;;
            (metadata)
              case $words[3] in
                (show)
                  _arguments -C \
                    '3:containers:__lxc_containers_all'
                ;;
                (edit)
                  _arguments -C \
                    '3:containers:__lxc_containers_all'
                ;;
              esac
              _arguments -C \
                '2: :__lxc_config_metadata_commands'
            ;;
            (template)
              case $words[3] in
                (list)
                  _arguments -C \
                    '3:containers:__lxc_containers_all'
                ;;
                (show|edit|delete)
                  if [ $#words[@] -gt 4 ]; then
                    _arguments -C \
                      "*:Templates:($(__get_lxc_templates_by_container $words[4]))"
                  fi
                  _arguments -C \
                    '3:containers:__lxc_containers_all'
                ;;
                (create)
                  _arguments -C \
                    '3:containers:__lxc_containers_all'
                ;;
              esac
              _arguments -C \
                '2: :__lxc_config_template_commands'
            ;;
            (device)
              case $words[3] in
                (add)
                  if [ $#words[@] -gt 4 ]; then
                    _arguments -C \
                      "*:keys:_sep_parts '$(__get_lxc_keys_by_device $words[5])' ="
                  fi
                  _arguments -C \
                    '3:containers:__lxc_containers_all' \
                    '4:device type:__lxc_device_types'
                ;;
                (get|unset)
                  if [ $#words[@] -eq 6 ]; then
                    local type
                    type=$(lxc config device get $words[4] $words[5] type)
                    _arguments -C "*:keys:($(__get_lxc_keys_by_device $type))"
                  fi
                  selected_container=$words[4]
                  _arguments -C \
                    '3:containers:__lxc_containers_all' \
                    '4:devices:__lxc_devices_by_container' \
                ;;
                (set)
                  if [ $#words[@] -eq 6 ]; then
                    local type
                    type=$(lxc config device get $words[4] $words[5] type)
                    _arguments -C "*:keys:($(__get_lxc_keys_by_device $type))"
                  fi
                  selected_container=$words[4]
                  _arguments -C \
                    '3:containers:__lxc_containers_all' \
                    '4:devices:__lxc_devices_by_container' \
                ;;
                (override)
                  if [ $#words[@] -gt 5 ]; then
                    local type
                    type=$(lxc config device get $words[4] $words[5] type)
                    _arguments -C \
                      "*:keys:_sep_parts '$(__get_lxc_keys_by_device $type)' ="
                  fi
                  selected_container=$words[4]
                  _arguments -C \
                    '3:containers:__lxc_containers_all' \
                    '4:devices:__lxc_devices_by_container' \
                ;;
                (show|list)
                  _arguments -C \
                    '3:containers:__lxc_containers_all' \
                ;;
                (remove)
                  selected_container=$words[4]
                  _arguments -C \
                    '3:containers:__lxc_containers_all' \
                    '4:devices:__lxc_devices_by_container' \
                ;;
              esac
              _arguments -C \
                '2: :__lxc_config_device_commands'
            ;;
            (trust)
              _arguments -C \
                '2: :__lxc_config_trust_commands'
              case $words[3] in
                (list|add|remove)
                  # complete remote
                  ;;
              esac
            ;;
          esac
          _arguments -C \
            '1: :__lxc_config_commands' \
            '--expanded[Show the expanded configuration]' \
            $opt_common
        ;;
        (console)
          _arguments -C \
            '1:containers:__lxc_containers_running' \
            $opt_common
        ;;
        (copy)
          # TODO: complete copy
        ;;
        (delete)
          _arguments -C \
            '*:containers:__lxc_containers_stopped' \
            $opt_common
        ;;
        (exec)
          _arguments -C \
            '1:containers:__lxc_containers_running' \
            '--env[An environment variable of the form HOME=/home/foo]' \
            '-T[Disable pseudo-terminal allocation]' \
            '-t[Force pseudo-terminal allocation]' \
            '-n[Disable stdin (reads from /dev/null)]' \
            '--mode=[Override the terminal mode (default: auto)]:mode:(auto interactive non-interactive)' \
            $opt_common
        ;;
        (file)
          case $words[2] in
            (push)
              _arguments -C \
                '2:files:_files' \
                '3:containers:__lxc_containers_all'
            ;;
            (pull)
              _arguments -C \
                '2:containers:__lxc_containers_all' \
                '3:files:_files'
            ;;
            (edit)
              _arguments -C \
                '2:containers:__lxc_containers_all'
            ;;
            (delete)
              _arguments -C \
                '*:containers:__lxc_containers_all'
            ;;
          esac
          _arguments -C \
            '1: :__lxc_file_commands' \
            "--gid=[Set the file's gid on push]" \
            "--uid=[Set the file's uid on push]" \
            "--mode=[Set the file's perms on push]" \
            ''{-p,--create-dirs}'[Create any directories necessary]' \
            ''{-r,--recursive}'[Recursively push or pull files]' \
            $opt_common
        ;;
        (help)
          _arguments -C \
            '1: :__lxc_commands' \
            $opt_common
        ;;
        (image)
          case $words[2] in
            (alias)
              case $words[3] in
                (create)
                  _arguments -C \
                    '4: :__lxc_images_as_fingerprint'
                ;;
                (delete|rename)
                  _arguments -C \
                    '3: :__lxc_images_as_alias'
                ;;
              esac
              _arguments -C \
                '2: :__lxc_image_alias_commands'
            ;;
            (delete|refresh)
              _arguments -C \
                '*: :__lxc_images_as_fingerprint'
            ;;
            (edit|show|info|export)
              _arguments -C \
                '2: :__lxc_images_as_fingerprint'
            ;;
            (copy)
              _arguments -C \
                '2: :__lxc_remotes'
            ;;
            (import)
              _arguments -C \
                '2: :_files'
            ;;
            (list)
              l="'Columns' 'l[Shortest image alias (and optionally number of other aliases)]' \
                'L[Newline-separated list of all image aliases]' 'f[Fingerprint]' \
                'p[Whether image is public]' 'd[Description]' 'a[Architecture]' \
                's[Size]' 'u[Upload date]'"
              _arguments -C \
                '2: :()' \
                {-c,--columns=}"[Columns (default lfpdasu)]:Columns:_values -s '' $l" \
                '--format=[Format]:Format:(csv json table yaml)' \
                $opt_common
            ;;
          esac
          _arguments -C \
            '1: :__lxc_image_commands' \
        ;;
        (info)
          _arguments -C \
            '1:containers:__lxc_containers_all' \
            '--resources[Show the resources available to the server]' \
            "--show-log[Show the container's last 100 log lines?]" \
            $opt_common
        ;;
        (launch|init)
          _arguments -C \
            '1: :__lxc_images' \
            {-c,--config=}'[Config key/value to apply to the new container (= map\[\])]' \
            {-e,--ephemeral}'[Ephemeral container]' \
            {-n,--network=}'[Network name]' \
            {-p,--profile=}'[Profile to apply to the new container]' \
            {-s,--storage=}'[Storage pool name]' \
            '--target=[Node name]' \
            {-t,--type=}'[Instance type]' \
            $opt_common
        ;;
        (list)
          local l
          # c.f https://github.com/lxc/lxd/blob/master/lxc/list.go
          l="'Columns' '4[IPv4 address]' '6[IPv6 address]' 'a[Architecture]' 'b[Storage pool]' \
            'c[Creation date]' 'd[Description]' 'l[Last used date]' 'n[Name]' 'N[Number of Processes]' \
            \"p[PID of the container's init process]\" 'P[Profiles]' 's[State]' \
            'S[Number of snapshots]' 't[Type (persistent or ephemeral)]' \
            'L[Location of the container (e.g. its cluster member)]'"
          _arguments -C \
            {-c,--columns=}"[Columns (default: ns46tSL)]:Columns:_values -s '' $l" \
            '--fast[Fast mode (same as --columns=nsacPt)]' \
            '--format=[Format (default: table)]:Format:(csv json table yaml)' \
            '--no-alias[Ignore aliases when determining what command to run]' \
            $opt_common
        ;;
        (manpage)
          _arguments -C \
            '1:Directory to put manpage to:_files' \
            $opt_common
        ;;
        (monitor)
          _arguments -C \
            '--loglevel=[Minimum level for log messages]:Log level:(info)' \
            '--pretty[Pretty rendering]' \
            '--type=[Event type to listen for]:Type:(lifecycle logging)' \
            $opt_common
        ;;
        (move)
          # TODO: complete move
          _arguments -C \
            $opt_common
        ;;
        (network)
          _arguments -C \
            '1: :__lxc_network_commands' \
            $opt_common
        ;;
        (operation)
          # TODO: complete operation
          _arguments -C \
            '1: :__lxc_operation_commands' \
            $opt_common
        ;;
        (pause)
          _arguments -C \
            '--all[Run command against all containers]' \
            '1: :__lxc_containers_running' \
            $opt_common
        ;;
        (profile)
          case $words[2] in
            (add)
              _arguments -C \
                '2: :__lxc_containers_all' \
                '3: :__lxc_profiles' \
            ;;
            (assign|apply)
              _arguments -C \
                '2: :__lxc_containers_all' \
                "3: :_values -s , Profiles $(__get_lxc_profiles)" \
            ;;
            (copy|cp)
              _arguments -C \
                '2: :__lxc_profiles' \
                '3: :__lxc_profiles' \
            ;;
            (delete|rm|edit|show)
              _arguments -C \
                '2: :__lxc_profiles' \
            ;;
            (get|set|unset)
              # TODO: complete profile keys
              _arguments -C \
                '2: :__lxc_profiles' \
            ;;
            (list|ls)
              # TODO: complete container profiles
              _arguments -C \
                '2: :__lxc_remotes' \
            ;;
            (rename|mv)
              # TODO: complete container profiles
              _arguments -C \
                '2: :__lxc_profiles' \
            ;;
          esac
          _arguments -C \
            '1: :__lxc_profile_commands' \
            $opt_common
        ;;
        (publish)
          # TODO: complete publish
          _arguments -C \
            '1: :__lxc_containers_all' \
            $opt_common
        ;;
        (query)
          # TODO: complete query
          _arguments -C \
            $opt_common
        ;;
        (remote)
          case $words[2] in
            (set-default|set-url|rename|remove)
              _arguments -C \
                '2: :__lxc_remotes' \
                $opt_common
            ;;
            (add)
              _arguments -C \
                '2: :()' \
                '--accept-certificate[Accept certificate]' \
                '--auth-type=[Server authentication type (tls or macaroons)]:Auth type:(tls macaroons)' \
                '--password[Remote admin password]' \
                '--protocol=[Server protocol (lxd or simplestreams)]:Protocol:(tls macaroons)' \
                '--public[Public image server]' \
            ;;
          esac
          _arguments -C \
            '1: :__lxc_remote_commands' \
            $opt_common
        ;;
        (rename)
          _arguments -C \
            '1:containers:__lxc_containers_stopped' \
            $opt_common
        ;;
        (restart)
          _arguments -C \
            '*:containers:__lxc_containers_running' \
            {-f,--force}'[Force the container to shutdown]' \
            '--timeout[Time to wait for the container before killing it]' \
            $opt_container_control \
            $opt_common
        ;;
        (restore)
          _arguments -C \
            '1:containers:__lxc_containers_all' \
            '--stateful[Store the container state (only for stop)]' \
            $opt_common
        ;;
        (restart)
          _arguments -C \
            '*:containers:__lxc_containers_running' \
            $opt_container_control \
            $opt_common
        ;;
        (snapshot)
          _arguments -C \
            '1:containers:__lxc_containers_all' \
            $opt_common
        ;;
        (start)
          _arguments -C \
            '*:containers:__lxc_containers_stopped' \
            $opt_container_control \
            $opt_common
        ;;
        (stop)
          _arguments -C \
            '*:containers:__lxc_containers_running' \
            '--timeout[Time to wait for the container before killing it (= -1)]' \
            {-f,--force}'[Force the container to shutdown]' \
            $opt_container_control \
            $opt_common
        ;;
        (storage)
          case $words[2] in
            (delete|edit|get|info|set|unset)
              _arguments -C \
                '2: :__lxc_storages'
            ;;
            (volume)
              case $words[3] in
                (list)
                  _arguments -C \
                    '3: :__lxc_storages'
                ;;
              esac
              _arguments -C \
                '2: :__lxc_storage_volume_commands'
            ;;
          esac
          _arguments -C \
            '1: :__lxc_storage_commands' \
            $opt_common
        ;;
      esac
    ;;
  esac
  return 0
}

__lxc_commands() {
  local -a _c
  _c=(
    'alias:Manage command aliases'
    'cluster:Manage cluster nodes'
    'config:Change container or server configuration options'
    "console:Interact with the container's console device and log"
    'copy:Copy containers within or in between LXD instances'
    'delete:Delete containers and snapshots'
    'exec:Execute commands in containers'
    'file:Manage files in containers'
    'finger:Check if the LXD server is alive'
    'image:Manipulate container images'
    'info:Show container or server information'
    'init:Create containers from images'
    'launch:Create and start containers from images'
    'list:List the existing containers'
    'manpage:Generate all the LXD manpages'
    'monitor:Monitor a local or remote LXD server'
    'move:Move containers within or in between LXD instances'
    'network:Manage and attach containers to networks'
    'operation:List, show and delete background operations'
    'pause:Pause containers'
    'profile:Manage container configuration profiles'
    'publish:Publish containers as images'
    'query:Send a raw query to LXD'
    'remote:Manage the list of remote LXD servers'
    'rename:Rename a container or snapshot'
    'restart:Restart containers'
    'restore:Restore containers from snapshots'
    'snapshot:Create container snapshots'
    'start:Start containers'
    'stop:Stop containers'
    'storage:Manage storage pools and volumes'
    'version:Print the version number of this client tool'
  )
  _describe -t commands Commands _c
}

__lxc_alias_commands() {
  local -a _c
  _c=(
    'add:Add a new alias <alias> pointing to <target>.'
    'remove:Remove the alias <alias>.'
    'list:List all the aliases.'
    'rename:Rename remote <old alias> to <new alias>.'
  )
  _describe -t commands 'Alias commands' _c
}

__lxc_cluster_commands() {
  local -a _c
  _c=(
    'list:List all nodes in the cluster'
    'show:Show details of a node'
    'rename:Rename a cluster node'
    'delete:Delete a node from the cluster.'
  )
  _describe -t commands 'Cluster commands' _c
}

__lxc_config_commands() {
  local -a _c
  _c=(
    'get:Get container or server configuration key'
    'set:Set container or server configuration key'
    'unset:Unset container or server configuration key'
    'show:Show container or server configuration'
    'edit:Edit container or server configuration key'

    'metadata:Container metadata'
    'template:Container templates'
    'device:Device management'
    'trust:Client trust store management'
  )
  _describe -t commands 'Config commands' _c
}

__lxc_config_device_commands() {
  local -a _c
  _c=(
    'add:Add a device to a container'
    'get:Get a device property'
    'set:Set a device property'
    'unset:Unset a device property'
    'override:Copy a profile inherited device into local container config'
    'list:List devices for container'
    'show:Show full device details for container'
    'remove:Remove device from container'
  )
  _describe -t commands 'Config device commands' _c
}


__lxc_config_metadata_commands() {
  local -a _c
  _c=(
    'show:Show the container metadata.yaml content'
    'edit:Edit the container metadata.yaml, either by launching external editor or reading STDIN'
  )
  _describe -t commands 'Config metadata commands' _c
}

__lxc_config_template_commands() {
  local -a _c
  _c=(
    'list:List the names of template files for a container.'
    'show:Show the content of a template file for a container'
    'create:Add an empty template file for a container'
    'edit:Edit the content of a template file for a container, either by launching external editor or reading STDIN'
    'delete:Delete a template file for a container'

  )
  _describe -t commands 'Config template commands' _c
}

__lxc_config_trust_commands() {
  local -a _c
  _c=(
    'list:List all trusted certs'
    'add:Add certfile.crt to trusted hosts'
    'remove:Remove the cert from trusted hosts'
  )
  _describe -t commands 'Config trust commands' _c
}

__lxc_file_commands() {
  local -a _c
  _c=(
    'pull:Pull file from container'
    'push:Push file to container'
    'delete:Delete files in containers.'
    'edit:Edit files in containers using the default text editor'
  )
  _describe -t commands 'File commands' _c
}

__lxc_image_commands() {
  local -a _c
  _c=(
    'copy:Copy an image from one LXD daemon to another over the network'
    'delete:Delete one or more images from the LXD image store'
    'import:Import images into the image store'
    'export:Export an image from the LXD image store into a distributable tarball'
    'info:Print everything LXD knows about a given image'
    'list:List images in the LXD image store. Filters may be of the'
    'show:Yaml output of the user modifiable properties of an image'
    'edit:Edit image, either by launching external editor or reading STDIN'
    'alias:Manage alias for image in the LXD image store'
    'refresh:Refresh images'
  )
  _describe -t commands 'Image commands' _c
}

__lxc_image_alias_commands() {
  local -a _c
  _c=(
    'create:Create a new alias for an existing image'
    'rename:Rename an alias'
    'delete:Delete an alias'
    'list:List the aliases. Filters may be part of the image hash or part of the image alias name'
  )
  _describe -t commands 'Image alias commands' _c
}

__lxc_profile_commands() {
  local -a _c
  _c=(
    'add:Add profiles to containers'
    'assign:Assign sets of profiles to containers'
    'copy:Copy profiles'
    'create:Create profiles'
    'delete:Delete profiles'
    'device:Manage container devices'
    'edit:Edit profile configurations as YAML'
    'get:Get values for profile configuration keys'
    'list:List profiles'
    'remove:Remove profiles from containers'
    'rename:Rename profiles'
    'set:Set profile configuration keys'
    'show:Show profile configurations'
    'unset:Unset profile configuration keys'
  )
  _describe -t commands 'Profile commands' _c
}

__lxc_remote_commands() {
  local -a _c
  _c=(
    'add:Add the remote <remote> at <url>'
    'remove:Remove the remote <remote>'
    'list:List all remotes'
    'rename:Rename remote <old name> to <new name>'
    "set-url:Update <remote>'s url to <url>"
    'set-default:Set the default remote'
    'get-default:Print the default remote'
  )
  _describe -t commands 'Remote commands' _c
}

__lxc_network_commands() {
  local -a _c
  _c=(
    'attach:Attach network interfaces to containers'
    'attach-profile:Attach network interfaces to profiles'
    'create:Create new networks'
    'delete:Delete networks'
    'detach:Detach network interfaces from containers'
    'detach-profile:Detach network interfaces from profiles'
    'edit:Edit network configurations as YAML'
    'get:Get values for network configuration keys'
    'info:Get runtime information on networks'
    'list:List available networks'
    'list-leases:List DHCP leases'
    'rename:Rename networks'
    'set:Set network configuration keys'
    'show:Show network configurations'
    'unset:Unset network configuration keys'
  )
  _describe -t commands 'Network commands' _c
}

__lxc_operation_commands() {
  local -a _c
  _c=(
    'delete:Delete a background operation (will attempt to cancel)'
    'list:List background operations'
    'show:Show details on a background operation'
  )
  _describe -t commands 'Operation commands' _c
}

__lxc_storage_commands() {
  local -a _c
  _c=(
    'create:Create storage pools'
    'delete:Delete storage pools'
    'edit:Edit storage pool configurations as YAML'
    'get:Get values for storage pool configuration keys'
    'info:Show useful information about storage pools'
    'list:List available storage pools'
    'set:Set storage pool configuration keys'
    'show:Show storage pool configurations and resources'
    'unset:Unset storage pool configuration keys'
    'volume:Manage storage volumes'
  )
  _describe -t commands 'Storage commands' _c
}

__lxc_storage_volume_commands() {
  local -a _c
  _c=(
    'attach:Attach new storage volumes to containers'
    'attach-profile:Attach new storage volumes to profiles'
    'copy:Copy storage volumes'
    'create:Create new custom storage volumes'
    'delete:Delete storage volumes'
    'detach:Detach storage volumes from containers'
    'detach-profile Detach storage volumes from profiles'
    'edit:Edit storage volume configurations as YAML'
    'get:Get values for storage volume configuration keys'
    'list:List storage volumes'
    'move:Move storage volumes between pools'
    'rename:Rename storage volumes and storage volume snapshots'
    'restore:Restore storage volume snapshots'
    'set:Set storage volume configuration keys'
    'show:Show storage volum configurations'
    'snapshot:Snapshot storage volumes'
    'unset:Unset storage volume configuration keys'
  )
  _describe -t commands 'Storage volume commands' _c


}

__lxc_devices_by_container () {
  local -a _devices
  _devices=(${(f)"$(_call_program devices lxc config device list $selected_container)"})
  _describe "Devices of $selected_container" _devices
}

__lxc_list_columns () {
  local -a l
  l=(
    '4:IPv4 address'
    '6:IPv6 address'
    'a:Architecture'
    'b:Storage pool'
    'c:Creation date'
    'd:Description'
    'l:Last used date'
    'n:Name'
    'N:Number of Processes'
    "p:PID of the container's init process"
    'P:Profiles'
    's:State'
    'S:Number of snapshots'
    't:Type (persistent or ephemeral)'
    'L:Location of the container (e.g. its cluster member)'
  )
  _describe "Columns" l
}

__lxc_device_types () {
  local -a _c
  _c=(
    'none:Inheritance blocker'
    'nic:Network interface'
    'disk:Mountpoint inside the container'
    'unix-char:Unix character device'
    'unix-block:Unix block device'
    'usb:USB device'
    'gpu:GPU device'
    'infiniband:Infiniband device'
    'proxy:Proxy device'
  )
  _describe 'Device types' _c
}

__lxc_containers_running () {
  local -a _containers
  _containers=(${(@f)"$(_call_program containers lxc list --fast | tail -n +4 | awk '{print $2 $4}' | egrep 'RUNNING$' | sed -e "s/RUNNING//")"})
  _describe -t containers 'Running Containers' _containers
}

__lxc_containers_stopped () {
  local -a _containers
  _containers=(${(@f)"$(_call_program containers lxc list --fast | tail -n +4 | awk '{print $2 $4}' | egrep 'STOPPED$' | sed -e "s/STOPPED//")"})
  _describe -t containers 'Stopped containers' _containers
}

__lxc_containers_all () {
  local -a _containers
  _containers=(${(@f)"$(_call_program containers lxc list --fast | tail -n +4 | awk '{print $2}' | egrep -v '^(\||^$)')"})
  _describe -t containers 'All containers' _containers
}

__lxc_images_as_fingerprint () {
  local -a _l
  _l=(${(@f)"$(_call_program images lxc image list | tail -n +4 | egrep -e '^\|' | awk '{split($0,a,"|"); for(i in a){ sub(/^[\ \t]+/,"",a[i]);sub(/[\ \t]+$/,"",a[i]);}; (a[2]=="") ? b=" (not aliased)" : b=" (aliased as " a[2] ")" ; print a[3] ":" a[5] b }' )"})
  _describe -t images_as_fingerprint 'Images as fingerprint' _l
}

__lxc_images_as_alias () {
  _l=(${(@f)"$(_call_program images lxc image alias list | tail -n +4 | egrep -e '^\|' | awk '{split($0,a,"|"); for(i in a){ sub(/^[\ \t]+/,"",a[i]);sub(/[\ \t]+$/,"",a[i]);}; (a[4]=="") ? b="" : b=" - " a[4] ;print a[2] ":" a[3] b }' )"})
  _describe -t images_as_alias 'Images as alias' _l
}

__lxc_images () {
  __lxc_images_as_alias
  __lxc_images_as_fingerprint
}

__lxc_aliases () {
  local -a _l
  _l=(${(@f)"$(_call_program aliases lxc alias list | tail -n +4 | egrep '^\|' | awk '{print $2 ":" $4}')"})
  _describe -t aliases 'Aliases' _l
}

__lxc_remotes () {
  local -a _l
  _l=(${(@f)"$(_call_program remotes lxc remote list | tail -n +4 | egrep -e '^\|' | sed -e 's/(default)//' | awk '{print $2 ":" $4}')"})
  _describe -t remotes 'Remotes' _l
}

__lxc_profiles () {
  local -a _l
  _l=(${(@f)"$(_call_program profiles lxc profile list | tail -n +4 | egrep -e '^\|' | awk '{print $2 ":used by " $4 " containers" }' )"})
  _describe -t profiles 'Profiles' _l
}

__lxc_storages () {
  local -a _l
  _l=(${(@f)"$(_call_program profiles lxc storage list | tail -n +4 | egrep -e '^\|' | awk '{print $2 }')"})
  _describe -t storages 'Storages' _l
}

__get_lxc_keys_by_device () {
  typeset -A hash
  hash[nic]="(nictype limits.ingress limits.egress limits.max name host_name hwaddr mtu \
    parent vlan ipv4.address ipv6.address security.mac_filtering maas.subnet.ipv4 maas.subnet.ipv6)"
  hash[infiniband]="(nictype name hwaddr mtu parent)"
  hash[disk]="(limits.read limits.write limits.max path source optional readonly size recursive pool)"
  hash[unix-char]="(source path major minor uid gid mode required)"
  hash[unix-block]="(source path major minor uid gid mode required)"
  hash[usb]="(vendorid productid uid gid mode required)"
  hash[gpu]="(vendorid productid id pci uid gid mode)"
  hash[proxy]="(listen bind connect)"
  echo "$hash[$1]"
}

__get_lxc_templates_by_container () {
  lxc config template list $1 | tail -n +4 | egrep '^\|' | awk '{print $2}'
}

__get_lxc_profiles () {
  lxc profile list | tail -n +4 | egrep -e '^\|' | awk '{print $2}' | paste -sd " "
}

compdef _lxc lxc
